#######################
# Document Properties #
#######################

SET DOCUMENT Licenses    = "MIT License"
SET DOCUMENT ContactInfo = "farah.humayun@scai-extern.fraunhofer.de"

SET DOCUMENT Name        = "heme_28716864.bel"
SET DOCUMENT Version     = "1.0.0"
SET DOCUMENT Authors     = "Farah Humayun"
SET DOCUMENT Description = "Heme BEL model is a disease model based on Biological Expression Language (BEL) containing the core mechanisms of heme as modulator in Hemolytic diseases such as sickle-cell anemia, beta-thalassemia, malaria and autoimmune hemolytic anemia."

##############
# Namespaces #
##############

# Namespaces hosted on https://github.com/pharmacome/terminology
# --------------------------------------------------------------
DEFINE NAMESPACE CHEBI          AS URL "https://raw.githubusercontent.com/pharmacome/terminology/b46b65c3da259b6e86026514dfececab7c22a11b/external/chebi-names.belns"
DEFINE NAMESPACE DRUGBANK       AS URL "https://raw.githubusercontent.com/pharmacome/terminology/b46b65c3da259b6e86026514dfececab7c22a11b/external/drugbank-names.belns"
DEFINE NAMESPACE ECCODE         AS URL "https://raw.githubusercontent.com/pharmacome/terminology/b46b65c3da259b6e86026514dfececab7c22a11b/external/ec-code.belns"
# DEFINE NAMESPACE FB           AS URL "https://raw.githubusercontent.com/pharmacome/terminology/80171ae62cf43aa1fc8a6c326b94537ab342458c/external/fb-names.belns"
DEFINE NAMESPACE HBP            AS URL "https://raw.githubusercontent.com/pharmacome/terminology/3074b85b858455d8eeb76cfcdef685ced19bbe11/export/hbp-names.belns"
DEFINE NAMESPACE HGNC           AS URL "https://raw.githubusercontent.com/pharmacome/terminology/b46b65c3da259b6e86026514dfececab7c22a11b/external/hgnc-names.belns"
DEFINE NAMESPACE HGNCGENEFAMILY AS URL "https://raw.githubusercontent.com/pharmacome/terminology/3074b85b858455d8eeb76cfcdef685ced19bbe11/external/hgnc.genefamily-names.belns"
DEFINE NAMESPACE GO             AS URL "https://raw.githubusercontent.com/pharmacome/terminology/b46b65c3da259b6e86026514dfececab7c22a11b/external/go-names.belns"
DEFINE NAMESPACE INTERPRO       AS URL "https://raw.githubusercontent.com/pharmacome/terminology/f2f993e599694ab5ce989cc39d789a499f75db99/external/interpro-names.belns"
DEFINE NAMESPACE MESH           AS URL "https://raw.githubusercontent.com/pharmacome/terminology/01c9daa61012b37dd0a1bc962521ba51a15b38f1/external/mesh-names.belns"
DEFINE NAMESPACE MGI            AS URL "https://raw.githubusercontent.com/pharmacome/terminology/f2f993e599694ab5ce989cc39d789a499f75db99/external/mgi-names.belns"
DEFINE NAMESPACE MIRBASE        AS URL "https://raw.githubusercontent.com/pharmacome/terminology/b46b65c3da259b6e86026514dfececab7c22a11b/external/mirbase-names.belns"
DEFINE NAMESPACE NCBIGENE       AS URL "https://raw.githubusercontent.com/pharmacome/terminology/b46b65c3da259b6e86026514dfececab7c22a11b/external/entrez.belns"
DEFINE NAMESPACE PFAM           AS URL "https://raw.githubusercontent.com/pharmacome/terminology/8ccfed235e418e4c8aa576f9a5ef0f838e794c7f/external/pfam-names.belns"
DEFINE NAMESPACE RGD            AS URL "https://raw.githubusercontent.com/pharmacome/terminology/f2f993e599694ab5ce989cc39d789a499f75db99/external/rgd-names.belns"

# Namespaces hosted on https://github.com/sorgerlab/famplex
# ---------------------------------------------------------
DEFINE NAMESPACE FPLX           AS URL "https://raw.githubusercontent.com/sorgerlab/famplex/e8ae9926ff95266032cb74f77973c84939bffbeb/export/famplex.belns"

# Namespaces hosted on https://arty.scai.fraunhofer.de/artifactory/bel/namespace
# ------------------------------------------------------------------------------
DEFINE NAMESPACE CTO            AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/clinical-trial-ontology/clinical-trial-ontology-1.0.0.belns"
DEFINE NAMESPACE DO             AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/disease-ontology/disease-ontology-20170725.belns"
DEFINE NAMESPACE HP             AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/hp/hp-20171108.belns"
DEFINE NAMESPACE SCOMP          AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/selventa-named-complexes/selventa-named-complexes-20170725.belns"
DEFINE NAMESPACE SFAM           AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/selventa-protein-families/selventa-protein-families-20170725.belns"

# Namespaces defined with regular expressions
# -------------------------------------------
DEFINE NAMESPACE DBSNP          AS PATTERN "rs[0-9]+"
DEFINE NAMESPACE TAXONOMY       AS PATTERN "^\d+$"
DEFINE NAMESPACE UNIPROT        AS PATTERN "^([A-N,R-Z][0-9]([A-Z][A-Z, 0-9][A-Z, 0-9][0-9]){1,2})|([O,P,Q][0-9][A-Z, 0-9][A-Z, 0-9][A-Z, 0-9][0-9])(\.\d+)?$"
DEFINE NAMESPACE PUBCHEM        AS PATTERN "^\d+$"

DEFINE NAMESPACE HM        AS PATTERN ".*"

###############
# Annotations #
###############

# Annotations defined with regular expressions
# --------------------------------------------
DEFINE ANNOTATION Species       AS PATTERN ".*"
DEFINE ANNOTATION TimePoint     AS PATTERN ".*"
DEFINE ANNOTATION Concentration     AS PATTERN ".*"

# Annotations hosted on https://arty.scai.fraunhofer.de/artifactory/bel/annotation
# --------------------------------------------------------------------------------
DEFINE ANNOTATION Anatomy       AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/anatomy/anatomy-20170511.belanno"
DEFINE ANNOTATION CellLine      AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/cell-line/cell-line-20170511.belanno"
DEFINE ANNOTATION CellStructure AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/cell-structure/cell-structure-20170511.belanno"
DEFINE ANNOTATION Cell          AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/cell/cell-20170511.belanno"
DEFINE ANNOTATION Confidence    AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/confidence/confidence-1.0.0.belanno"
DEFINE ANNOTATION Disease       AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/disease/disease-20170511.belanno"
DEFINE ANNOTATION Gender        AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/gender/gender-1.0.0.belanno"
DEFINE ANNOTATION MeSHAnatomy   AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/mesh-anatomy/mesh-anatomy-20170511.belanno"
DEFINE ANNOTATION MeSHDisease   AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/mesh-diseases/mesh-diseases-20170511.belanno"
DEFINE ANNOTATION Subgraph      AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/neurommsig/neurommsig-1.0.3.belanno"
DEFINE ANNOTATION TextLocation  AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/text-location/text-location-1.0.1.belanno"

##############
# Statements #
##############

SET Citation = {"PubMed", "J Leukoc Biol. 2017 Oct;102(4):1127-1141. doi: 10.1189/jlb.3A0317-124R. Epub 2017 Jul 17.", "28716864"}

SET TextLocation = "Introduction"

SET Evidence = "Hemin induces expression of the
adhesion molecules on endothelial cells [7, 8] and enables firm
neutrophil attachment to the endothelium and initiation of an
inflammatory response [9, 10]."

SET Cell = "endothelial cell"
a(CHEBI:heme) pos a(MESH:"Cell Adhesion Molecules")
a(CHEBI:heme) pos bp(GO:"inflammatory response")

SET Evidence = "With the
triggering of the oxidative burst and modification of actin
cytoskeleton dynamics, hemin also induces neutrophil migration
[10]"

a(CHEBI:heme) -> bp(GO:"neutrophil migration")

SET TextLocation = "Results"

SET Evidence = "Under the same experimental conditions, addition of 1 mg/
ml A1AT significantly prevented hemin-induced neutrophil spreading
and adhesion (Fig. 3A)."

p(HGNC:SERPINA1) -| bp(GO:"neutrophil migration")
SET Cell = "neutrophil"
p(HGNC:SERPINA1) -| bp(MESH:"Cell Adhesion")

SET Evidence = "Based on the previous findings that hemin induces neutrophil
adhesion to endothelial cells [8] and that A1AT protects
endothelial cells from neutrophil adhesion induced by fMLP
[27], we investigated whether A1AT, as a scavenger of hemin, can
prevent hemin-induced neutrophil adhesion to HUVECs. As
shown in Fig. 4, neutrophils treated with hemin or fMLP (used as
a positive control) exhibited a 3-fold higher adhesion to HUVECs
compared with controls. However, the adherence of neutrophils
treated with hemin/A1AT did not differ from controls (Fig. 4)."

SET Cell = "endothelial cell"
a(CHEBI:heme) pos a(MESH:"Cell Adhesion Molecules")
p(HGNC:SERPINA1) -| a(MESH:"Cell Adhesion Molecules")

SET Evidence = "In contrast, hemin
strongly increased the percentage of neutrophils positive for surface
expression of vimentin [mean (SD) 48.8% (20) vs. 2.5% (0.8), n = 5;
P , 0.001]."

a(CHEBI:heme) -> p(HGNC:VIM)

SET Evidence = "In the presence of A1AT, this latter effect of hemin was
significantly inhibited and did not differ from controls (Fig. 5B)."

p(HGNC:SERPINA1) -| p(HGNC:VIM)

SET Evidence = "Incubation of neutrophils
with 4 mM hemin resulted in an ;2-fold increase in CXCL8
mRNA expression and in a significant increase in released and
cell-associated levels of IL-8 protein compared with the nontreated
controls or A1AT-treated cells (Fig. 7Aâ€“C)."

a(CHEBI:heme) -> r(HGNC:CXCL8)
a(CHEBI:heme) -> p(HGNC:CXCL8)

SET Evidence = "In the
presence of A1AT, hemin-induced release of IL-8 protein was
inhibited significantly (Fig. 7B)."

p(HGNC:SERPINA1) -| p(HGNC:CXCL8)

SET Evidence = "As expected, incubation of neutrophils with hemin
resulted in a significant number of cells producing ROS (Fig. 8A and
B)."

SET Cell = "neutrophil"
a(CHEBI:heme) pos a(MESH:"Reactive Oxygen Species")

SET Evidence = "The ability of hemin to trigger ROS production in neutrophils
was abrogated significantly in the presence of A1AT (Fig. 8)."

SET Cell = "neutrophil"
p(HGNC:SERPINA1) -| a(MESH:"Reactive Oxygen Species")

SET Evidence = "As predicted, HMOX1 expression levels
(means 6 SD) in hemin-treated cells were much higher (7.25 6
5.02, n = 12; P , 0.001) than in nontreated controls (0.14 6 0.18,
n = 9) or A1AT-treated cells (0.09 6 0.1, n = 9)."

a(CHEBI:heme) pos p(HGNC:HMOX1)

SET Evidence = "In the presence
of A1AT, hemin effect on HMOX1 expression was diminished
significantly (Fig. 9A)."

p(HGNC:SERPINA1) -| p(HGNC:HMOX1)

SET Evidence = "As
shown in Fig. 10, when neutrophils were incubated for 2 h with
hemin, GR activity decreased by 50% (P , 0.05)."

SET Cell = "neutrophil"
a(CHEBI:heme) -| act(p(HGNC:NR3C1))

SET Evidence = "Under the same
experimental conditions, A1AT blocked the ability of hemin to
reduce GR activity (Fig. 10)."

SET Cell = "neutrophil"
p(HGNC:SERPINA1) pos act(p(HGNC:NR3C1))

SET Evidence = "As shown in Fig. 11A, in hemin-treated neutrophils,
PKC activity increased by ;2.6-fold (P , 0.001) relative to controls."

SET Cell = "neutrophil"
a(CHEBI:heme) -> act(p(PFAM:"Pkinase_C"))

SET Evidence = "However, in the presence of A1AT, hemin did not change PKC
activity significantly."

p(HGNC:SERPINA1) neg act(p(PFAM:"Pkinase_C"))

SET Evidence = "Exposure of cells to hemin or hemin plus A1AT did not
change TLR2/4 mRNA levels (Fig. 12)."

a(CHEBI:heme) causesNoChange r(HGNC:TLR2)
a(CHEBI:heme) causesNoChange r(HGNC:TLR4)
complex(a(CHEBI:hemin), p(HGNC:SERPINA1)) causesNoChange r(HGNC:TLR2)
complex(a(CHEBI:hemin), p(HGNC:SERPINA1)) causesNoChange r(HGNC:TLR4)

SET Evidence = "Of note, hemin significantly
lowered LRP1 mRNA, whereas hemin/A1AT had no significant effect
on LRP1 expression relative to controls (Supplemental Fig. 2)."

a(CHEBI:heme) -> r(HGNC:LRP1)
complex(a(CHEBI:hemin), p(HGNC:SERPINA1)) causesNoChange r(HGNC:LRP1)

SET Evidence = "Therapy
with A1AT slightly reduced serum CXCL1/KC levels (Fig. 15)."

SET MeSHAnatomy = "Serum"
p(MGI:Serpina1a) -| p(MGI:Cxcl1)

SET TextLocation = "Discussion"

SET Evidence = "Free hemin is a cytotoxic molecule that mediates oxidative stress,
endothelial activation, and inflammation, and it is implicated in
malaria pathogenesis [40] and AKI, among others [41]."

SET MeSHDisease = "Malaria"|"Acute Kidney Injury"
a(CHEBI:heme) pos bp(MESH:"Oxidative Stress")
a(CHEBI:heme) pos bp(GO:"endothelial cell activation")
a(CHEBI:heme) pos path(MESH:Inflammation)

SET Evidence = "The main function of A1AT is to
inhibit neutrophil elastase and proteinase 3."

SET Cell = "neutrophil"
p(HGNC:SERPINA1) -| p(HGNC:ELANE)
p(HGNC:SERPINA1) -| p(HGNC:PRTN3)

SET Evidence = "However A1AT
has broader functions [26, 27], abrogating inflammation via
both enzyme-inhibitory and noninhibitory mechanisms [47]."

p(HGNC:SERPINA1) -| path(MESH:Inflammation)

SET Evidence = "For instance, earlier studies have demonstrated
that neutrophil elastase degrades the hemoglobin liberating
free hemin that induces ROS production."

SET Cell = "neutrophil"
p(HGNC:ELANE) -> deg(p(HGNC:HBB)) 
deg(p(HGNC:HBB)) -> a(CHEBI:heme)
a(CHEBI:heme) -> a(MESH:"Reactive Oxygen Species")

SET Evidence = "Along with this, A1AT inhibited hemin to
induce PKC phosphorylation, which is an essential step for the
production of ROS [9]."

p(HGNC:SERPINA1) -| p(PFAM:"Pkinase_C", pmod(Ph))

UNSET TextLocation
###########