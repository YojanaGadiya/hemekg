#######################
# Document Properties #
#######################
SET DOCUMENT Licenses    = "MIT License"
SET DOCUMENT ContactInfo = "farah.humayun@scai-extern.fraunhofer.de"

SET DOCUMENT Name        = "heme_26875449.bel"
SET DOCUMENT Version     = "1.0.2"
SET DOCUMENT Authors     = "Farah Humayun"
SET DOCUMENT Description = "Heme BEL model is a disease model based on Biological Expression Language (BEL) containing the core mechanisms of heme as modulator in Hemolytic diseases such as sickle-cell anemia, beta-thalassemia, malaria and autoimmune hemolytic anemia."

##############
# Namespaces #
##############

# Namespaces hosted on https://github.com/pharmacome/terminology
# --------------------------------------------------------------
DEFINE NAMESPACE CHEBI          AS URL "https://raw.githubusercontent.com/pharmacome/terminology/b46b65c3da259b6e86026514dfececab7c22a11b/external/chebi-names.belns"
DEFINE NAMESPACE DRUGBANK       AS URL "https://raw.githubusercontent.com/pharmacome/terminology/b46b65c3da259b6e86026514dfececab7c22a11b/external/drugbank-names.belns"
DEFINE NAMESPACE ECCODE         AS URL "https://raw.githubusercontent.com/pharmacome/terminology/b46b65c3da259b6e86026514dfececab7c22a11b/external/ec-code.belns"
# DEFINE NAMESPACE FB           AS URL "https://raw.githubusercontent.com/pharmacome/terminology/80171ae62cf43aa1fc8a6c326b94537ab342458c/external/fb-names.belns"
DEFINE NAMESPACE HBP            AS URL "https://raw.githubusercontent.com/pharmacome/terminology/3074b85b858455d8eeb76cfcdef685ced19bbe11/export/hbp-names.belns"
DEFINE NAMESPACE HGNC           AS URL "https://raw.githubusercontent.com/pharmacome/terminology/b46b65c3da259b6e86026514dfececab7c22a11b/external/hgnc-names.belns"
DEFINE NAMESPACE HGNCGENEFAMILY AS URL "https://raw.githubusercontent.com/pharmacome/terminology/3074b85b858455d8eeb76cfcdef685ced19bbe11/external/hgnc.genefamily-names.belns"
DEFINE NAMESPACE GO             AS URL "https://raw.githubusercontent.com/pharmacome/terminology/b46b65c3da259b6e86026514dfececab7c22a11b/external/go-names.belns"
DEFINE NAMESPACE INTERPRO       AS URL "https://raw.githubusercontent.com/pharmacome/terminology/f2f993e599694ab5ce989cc39d789a499f75db99/external/interpro-names.belns"
DEFINE NAMESPACE MESH           AS URL "https://raw.githubusercontent.com/pharmacome/terminology/01c9daa61012b37dd0a1bc962521ba51a15b38f1/external/mesh-names.belns"
DEFINE NAMESPACE MGI            AS URL "https://raw.githubusercontent.com/pharmacome/terminology/f2f993e599694ab5ce989cc39d789a499f75db99/external/mgi-names.belns"
DEFINE NAMESPACE MIRBASE        AS URL "https://raw.githubusercontent.com/pharmacome/terminology/b46b65c3da259b6e86026514dfececab7c22a11b/external/mirbase-names.belns"
DEFINE NAMESPACE NCBIGENE       AS URL "https://raw.githubusercontent.com/pharmacome/terminology/b46b65c3da259b6e86026514dfececab7c22a11b/external/entrez.belns"
DEFINE NAMESPACE PFAM           AS URL "https://raw.githubusercontent.com/pharmacome/terminology/8ccfed235e418e4c8aa576f9a5ef0f838e794c7f/external/pfam-names.belns"
DEFINE NAMESPACE RGD            AS URL "https://raw.githubusercontent.com/pharmacome/terminology/f2f993e599694ab5ce989cc39d789a499f75db99/external/rgd-names.belns"

# Namespaces hosted on https://github.com/sorgerlab/famplex
# ---------------------------------------------------------
DEFINE NAMESPACE FPLX           AS URL "https://raw.githubusercontent.com/sorgerlab/famplex/e8ae9926ff95266032cb74f77973c84939bffbeb/export/famplex.belns"

# Namespaces hosted on https://arty.scai.fraunhofer.de/artifactory/bel/namespace
# ------------------------------------------------------------------------------
DEFINE NAMESPACE CTO            AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/clinical-trial-ontology/clinical-trial-ontology-1.0.0.belns"
DEFINE NAMESPACE DO             AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/disease-ontology/disease-ontology-20170725.belns"
DEFINE NAMESPACE HP             AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/hp/hp-20171108.belns"
DEFINE NAMESPACE SCOMP          AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/selventa-named-complexes/selventa-named-complexes-20170725.belns"
DEFINE NAMESPACE SFAM           AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/selventa-protein-families/selventa-protein-families-20170725.belns"

# Namespaces defined with regular expressions
# -------------------------------------------
DEFINE NAMESPACE DBSNP          AS PATTERN "rs[0-9]+"
DEFINE NAMESPACE TAXONOMY       AS PATTERN "^\d+$"
DEFINE NAMESPACE UNIPROT        AS PATTERN "^([A-N,R-Z][0-9]([A-Z][A-Z, 0-9][A-Z, 0-9][0-9]){1,2})|([O,P,Q][0-9][A-Z, 0-9][A-Z, 0-9][A-Z, 0-9][0-9])(\.\d+)?$"
DEFINE NAMESPACE PUBCHEM        AS PATTERN "^\d+$"

DEFINE NAMESPACE HM        AS PATTERN ".*"

###############
# Annotations #
###############

# Annotations defined with regular expressions
# --------------------------------------------

DEFINE ANNOTATION Species       AS PATTERN ".*"
DEFINE ANNOTATION TimePoint     AS PATTERN ".*"
DEFINE ANNOTATION Concentration     AS PATTERN ".*"

# Annotations hosted on https://arty.scai.fraunhofer.de/artifactory/bel/annotation
# --------------------------------------------------------------------------------
DEFINE ANNOTATION Anatomy       AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/anatomy/anatomy-20170511.belanno"
DEFINE ANNOTATION CellLine      AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/cell-line/cell-line-20170511.belanno"
DEFINE ANNOTATION CellStructure AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/cell-structure/cell-structure-20170511.belanno"
DEFINE ANNOTATION Cell          AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/cell/cell-20170511.belanno"
DEFINE ANNOTATION Confidence    AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/confidence/confidence-1.0.0.belanno"
DEFINE ANNOTATION Disease       AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/disease/disease-20170511.belanno"
DEFINE ANNOTATION Gender        AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/gender/gender-1.0.0.belanno"
DEFINE ANNOTATION MeSHAnatomy   AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/mesh-anatomy/mesh-anatomy-20170511.belanno"
DEFINE ANNOTATION MeSHDisease   AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/mesh-diseases/mesh-diseases-20170511.belanno"
DEFINE ANNOTATION Subgraph      AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/neurommsig/neurommsig-1.0.3.belanno"
DEFINE ANNOTATION TextLocation  AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/text-location/text-location-1.0.1.belanno"

##############
# Statements #
##############

SET Citation = {"PubMed", "Trends Mol Med. 2016 Mar;22(3):200-213. doi: 10.1016/j.molmed.2016.01.004. Epub 2016 Feb.", "26875449"} 

SET TextLocation = "Review"

SET Evidence = "In cases of
congenital hemoglobinopathies such as sickle cell disease, deficiencies in complement
system regulators such as paroxysmal nocturnal hemoglobinuria, and many other disorders,
erythrocytes can lyse and liberate large quantities of hemoglobin."

path(HP:"Paroxysmal nocturnal hemoglobinuria") pos p(HGNC:HBB)

SET Evidence = "Extracellular hemoglobin and heme are pro-oxidative,
proinflammatory, and cytotoxic [10–12], and can contribute to the pathology of hemolytic
diseases."

p(HGNC:HBB) -> a(HM:"oxidative reactions")
p(HGNC:HBB) -> path(MESH:Inflammation)
p(HGNC:HBB) -> a(MESH:"Toxic Actions")
a(CHEBI:heme) -> a(HM:"oxidative reactions")
a(CHEBI:heme) -> path(MESH:Inflammation)
a(CHEBI:heme) -> a(MESH:"Toxic Actions")

SET Evidence = "Thus, the interactions of hemoglobin with haptoglobin, and of
heme with hemopexin, ensure safe disposal of potentially dangerous molecules [6,7,15–19]"

p(HGNC:HP) -| p(HGNC:HBB)
p(HGNC:HPX) -| a(CHEBI:heme)

SET Evidence = "In cases of extensive and chronic hemolysis, levels of haptoglobin and hemopexin
in plasma decrease markedly [20,21]."

path(MESH:Hemolysis) neg p(HGNC:HP)
path(MESH:Hemolysis) neg p(HGNC:HPX)

SET Evidence = "Excess extracellular
heme is weakly bound to albumin [22],
/1-microglobulin (which can also degrade heme) [23]
and, most likely, to other plasma proteins and lipoproteins; 
there is a dynamic nature to such interactions."

p(HGNC:AMBP) -| a(CHEBI:heme) 

SET Evidence = "Interestingly, a recent study demonstrated that, in severe hemolytic disease
(sickle cell anemia), at least one third of plasma heme was associated with membrane vesicle
structures (microparticles) that were generated from erythrocytes during hemolysis [24]."

path(MESH:Hemolysis) -> a(MESH:"Cell-Derived Microparticles")
bp(MESH:Eryptosis) -> a(MESH:"Cell-Derived Microparticles")

SET Evidence = "Most studies concerning the pathophysiological roles of heme have focused on the protective
effect of the heme-degrading enzyme, heme oxygenase 1 (HO-1) [25] (Box 2), and on the
effect of this danger-associated molecule on cells, leading to oxidative stress, TLR4 signaling
[26,27], and NLRP3 inflammasome activation [28] (Box 4)."

a(CHEBI:heme) -> bp(MESH:"Oxidative Stress")
a(CHEBI:heme) -> p(HGNC:NLRP3)
a(CHEBI:heme) -> p(HGNC:TLR4)

SET Evidence = "For example, one of the severe complications of
sickle-cell disease is vaso-occlusion, an outcome that can be triggered in mice upon heme
administration [27,32]."

SET MeSHDisease = "Anemia, Sickle Cell"
a(CHEBI:heme) -> path(HM:"vaso-occlusive crisis")

SET Evidence = "Heme is one of the factors capable of inducing stimulation and
damage of endothelium, promoting the recruitment of neutrophils and sickle-cell erythro-
cytes, and subsequently prompting a vaso-occlusive crisis."

a(CHEBI:heme) -> bp(GO:"endothelial cell activation")
a(CHEBI:heme) -> bp(GO:"neutrophil activation")
a(CHEBI:heme) -> path(HM:"vaso-occlusive crisis")

SET Evidence = "Moreover, in vitro studies have demonstrated that heme binds to human platelets
and induces platelet activation and aggregation [37–39]."

a(CHEBI:heme) -> bp(GO:"platelet activation")
a(CHEBI:heme) -> bp(MESH:"Platelet Aggregation")

SET Evidence = "Heme significantly reduced FVIII cofactor activity by 
inhibiting its interaction with activated factor IX(FIXa) in vitro."

a(CHEBI:heme) -| act(p(HGNC:F8))

SET Evidence = "In humans and mice, VWF protects FVIII from 
degradation by proteases in the circulation and controls FVIII
catabolism [43]."

p(HGNC:VWF) -| deg(p(HGNC:F8))

SET Evidence = "From another perspective, in vitro assays have demonstrated that heme can also bind to
fibrinogen and decrease its thrombin-mediated cleavage, thus affecting the
final common coagulation pathway and reducing
fibrin formation, important in clotting [44] (Figure 1)."

a(CHEBI:heme) -| a(MESH:Fibrin)

SET Evidence = "Heme
perturbs the enzymatic activity of thrombin, further contributing to the overall anticoagulant effect
and decreased
fibrin formation [45]."

a(CHEBI:heme) -| a(MESH:Fibrin)

SET Evidence = "In contrast to the anticoagulant effect of
heme on the intrinsic and terminal pathways of coagulation, heme can also trigger the extrinsic
pathway of coagulation by upregulating tissue factor (TF) on the surface of human endothelial
cells in vitro, as well as on leukocytes and perivascular cells in mice [50,51]."

a(CHEBI:heme) -> p(MGI:F3)

SET Evidence = "In the extrinsic
pathway of coagulation, upregulated TF binds to activated factor VII (FVIIa), thus leading to
activation of FX."

complex(p(HGNC:F3), p(HGNC:F7)) -> p(HGNC:F10)

SET Evidence = "Taken
sequentially, it appears that the release of heme under hemolytic conditions initiates the extrinsic
pathway of coagulation through the upregulation of TF on endothelial cells and leukocytes, but
subsequently blocks the propagation of coagulation by inhibiting FVIII and FV, and by inhibiting
the conversion of
fibrinogen into
fibrin and
fibrin clots."

a(CHEBI:heme) pos bp(GO:"blood coagulation, extrinsic pathway")
a(CHEBI:heme) -> p(HGNC:TNF)
a(CHEBI:heme) -| p(HGNC:F8)
a(CHEBI:heme) -| p(HGNC:F5)
a(CHEBI:heme) -| a(MESH:Fibrin)

SET Evidence = "For
example, the in vitro appearance of strong reactivity towards phospholipids of antibodies from
normal human plasma following exposure to heme suggests that heme, when released in vivo,
may perturb coagulation processes indirectly via its effects on antibodies."

a(CHEBI:heme) neg bp(MESH:"Antibody Specificity")

SET Evidence = "Hemolytic diseases are
often accompanied by dysregulation and overactivation of the complement system [72–74],
which may be induced by free extracellular heme [33,75–77] (Figure 3B)."

a(CHEBI:heme) -> bp(GO:"complement activation")

SET Evidence = "C3a and C5a
anaphylatoxins, as well as the soluble membrane attack complex (sC5b9), are generated
by incubation of heme with human serum or blood in vitro, via the alternative complement
pathway [33,77] (Box 5)."

a(CHEBI:heme) -> a(MESH:"Complement C3a")
a(CHEBI:heme) -> a(MESH:"Complement C5a")
a(CHEBI:heme) -> a(MESH:"Complement Membrane Attack Complex")

SET Evidence = "Furthermore, the incubation of serum or whole blood with heme
induces deposition of activation fragments (C3b, iC3b, C3dg) of complement component 3
(C3) at the surface of erythrocytes [77]."

SET Cell = "erythrocyte"
a(CHEBI:heme) -> p(HGNC:C3)

SET Evidence = "Indeed, the severe forms of malaria are associated with activation of the complement system [74]."

path(MESH:Malaria) -- bp(GO:"complement activation")

SET Evidence = "Plasmodium-mediated lysis of a single infected erythrocyte in vivo was shown to result in the
lysis of 8–10 uninfected cells [78,79]."

act(a(MESH:Plasmodium)) -> path(MESH:Hemolysis)

SET Evidence = "The mechanism behind this erythrocyte loss is not well
understood but may be related to reduced erythrocyte deformability, accelerated senescence,
or to complement or antibody-mediated (anti-erythrocyte) erythrophagocytosis [79]."

bp(MESH:"Erythrocyte Deformability") pos bp(MESH:"Erythrocyte Count")
a(HM:erythrophagocytosis) -| bp(MESH:"Erythrocyte Count")

SET Evidence = "It is
possible that heme-induced overactivation of the alternative complement pathway, and depo-
sition of C3 fragments on erythrocyte membranes, participate in erythrophagocytosis of
uninfected red blood cells in severe forms of malaria [77]."

SET MeSHDisease = "Malaria"
SET Cell = "erythrocyte"
a(CHEBI:heme) -> bp(GO:"complement activation, alternative pathway")
bp(GO:"complement activation, alternative pathway") pos a(HM:erythrophagocytosis)

SET Evidence = "This scenario may resemble that
of paroxysmal nocturnal hemoglobinuria patients, who can be treated with complement C5-
blocking antibody eculizumab [80]; in this example, extravascular hemolysis has been shown to
occur in a small number of these patients despite treatment. It has been suggested that this is
due to erythrophagocytosis of red blood cells deficient in the C3d-opsonized complement
regulators – CD55 and CD59 [81,82]."

p(HGNC:CD55) -| path(MESH:Hemolysis)
p(HGNC:CD59) -| path(MESH:Hemolysis)

SET Evidence = "Molecular docking predicted the interaction of heme in close proximity to a
thioester bond, known to be important for the activation of C3."

complex(a(CHEBI:heme), a(CHEBI:thioester)) -> p(HGNC:C3)

SET Evidence = "Heme exposure of other proteins in the alternative pathway (factors B, D, and H)
does not appear to have any impact on their functions, thus implying that heme activates the
alternative complement pathway by affecting C3 directly [33]."

a(CHEBI:heme) causesNoChange p(HGNC:CFB) 
a(CHEBI:heme) causesNoChange p(HGNC:CFD) 
a(CHEBI:heme) causesNoChange p(HGNC:CFH) 
a(CHEBI:heme) -> bp(GO:"complement activation, alternative pathway")

SET Evidence = "Moreover, exposure of endothelial cells to heme results in diminished
surface expression of the negative complement regulators decay-accelerating factor
(DAF) and membrane cofactor protein (MCP) [33], and hence reduces protection against
complement."

a(CHEBI:heme) -| p(HGNC:CD55)
a(CHEBI:heme) -| p(HGNC:CD46)

SET Evidence = "However, in contrast to its effect on C3, exposure of C1q to heme was
demonstrated to inhibit its functions in vitro [89,90] (Figure 3B)."

complex(a(CHEBI:heme), p(PFAM:C1q)) -| act(p(HGNC:C3))

SET Evidence= "Heme could suppress C1q
binding to its main ligands, immunoglobulins and C-reactive protein (CRP), in a concentrationdepended
manner."

a(CHEBI:heme) -| act(p(PFAM:C1q))

SET Evidence = "The inhibitory effect of heme on the classical pathway is in
agreement with in vitro functional assays showing that heme is capable of activating only the
alternative pathway in human sera."

SET MeSHAnatomy = "Serum"
a(CHEBI:heme) -| bp(GO:"complement activation, classical pathway")
a(CHEBI:heme) -> bp(GO:"complement activation, alternative pathway")

SET Evidence = "In contrast to its overactivating effects on the alternative pathway, heme
inhibits the classical pathway. It binds to C1q, alters its electrostatic properties, and hampers the recognition of target
molecules. This results in a reduction of classical pathway C3 convertase formation and C3b deposition."

a(CHEBI:heme) -| bp(GO:"complement activation, classical pathway")
a(CHEBI:heme) -> bp(GO:"complement activation, alternative pathway")
a(CHEBI:heme) -| act(p(PFAM:C1q))

SET Evidence = "Heme may
be implicated and contribute to the development of (i) bp(MESH:"Hemostasis")angiopthy in patients with
aHUS [33], (ii) vaso-occlusion in sickle-cell disease [18,24,27,32], (iii) vascular injury and kidney
dysfunction in transfusion of old blood [16], (iv) atherosclerosis [92], or (v) erythrocyte loss in
malaria [77]."

SET MeSHDisease = "Atypical Hemolytic Uremic Syndrome"
a(CHEBI:heme) -> path(MESH:"Thrombotic Microangiopathies")

SET MeSHDisease = "Anemia, Sickle Cell"
a(CHEBI:heme) -> path(HM:"vaso-occlusive crisis")
a(CHEBI:heme) -> path(MESH:"Vascular System Injuries")
a(CHEBI:heme) -> path(MESH:"Acute Kidney Injury")

a(CHEBI:heme) -> path(MESH:Atherosclerosis)

SET MeSHDisease = "Malaria"
a(CHEBI:heme) -| bp(MESH:"Erythrocyte Count")

SET Evidence = "Despite its damaging effects, heme induces the expression of HO-1, which degrades heme to
anti-inflammatory, cytoprotective, and antioxidant products [25]."

a(CHEBI:heme) -> p(HGNC:HMOX1)
p(HGNC:HMOX1) -> deg(a(CHEBI:heme))

SET Evidence = "A recent Phase IIB clinical trial showed that preconditioning using hemin
upregulated HO-1 in renal transplantation, launching further studies on clinical outcome [97]."

a(CHEBI:heme) ->  p(HGNC:HMOX1)

UNSET TextLocation
################
