#######################
# Document Properties #
#######################

SET DOCUMENT Licenses    = "MIT License"
SET DOCUMENT ContactInfo = "farah.humayun@scai-extern.fraunhofer.de"

SET DOCUMENT Name        = "heme_27308950.bel"
SET DOCUMENT Version     = "1.0.1"
SET DOCUMENT Authors     = "Farah Humayun"
SET DOCUMENT Description = "Heme BEL model is a disease model based on Biological Expression Language (BEL) containing the core mechanisms of heme as modulator in Hemolytic diseases such as sickle-cell anemia, beta-thalassemia, malaria and autoimmune hemolytic anemia."

##############
# Namespaces #
##############

# Namespaces hosted on https://github.com/pharmacome/terminology
# --------------------------------------------------------------
DEFINE NAMESPACE CHEBI          AS URL "https://raw.githubusercontent.com/pharmacome/terminology/b46b65c3da259b6e86026514dfececab7c22a11b/external/chebi-names.belns"
DEFINE NAMESPACE DRUGBANK       AS URL "https://raw.githubusercontent.com/pharmacome/terminology/b46b65c3da259b6e86026514dfececab7c22a11b/external/drugbank-names.belns"
DEFINE NAMESPACE ECCODE         AS URL "https://raw.githubusercontent.com/pharmacome/terminology/b46b65c3da259b6e86026514dfececab7c22a11b/external/ec-code.belns"
# DEFINE NAMESPACE FB           AS URL "https://raw.githubusercontent.com/pharmacome/terminology/80171ae62cf43aa1fc8a6c326b94537ab342458c/external/fb-names.belns"
DEFINE NAMESPACE HBP            AS URL "https://raw.githubusercontent.com/pharmacome/terminology/3074b85b858455d8eeb76cfcdef685ced19bbe11/export/hbp-names.belns"
DEFINE NAMESPACE HGNC           AS URL "https://raw.githubusercontent.com/pharmacome/terminology/b46b65c3da259b6e86026514dfececab7c22a11b/external/hgnc-names.belns"
DEFINE NAMESPACE HGNCGENEFAMILY AS URL "https://raw.githubusercontent.com/pharmacome/terminology/3074b85b858455d8eeb76cfcdef685ced19bbe11/external/hgnc.genefamily-names.belns"
DEFINE NAMESPACE GO             AS URL "https://raw.githubusercontent.com/pharmacome/terminology/b46b65c3da259b6e86026514dfececab7c22a11b/external/go-names.belns"
DEFINE NAMESPACE INTERPRO       AS URL "https://raw.githubusercontent.com/pharmacome/terminology/f2f993e599694ab5ce989cc39d789a499f75db99/external/interpro-names.belns"
DEFINE NAMESPACE MESH           AS URL "https://raw.githubusercontent.com/pharmacome/terminology/01c9daa61012b37dd0a1bc962521ba51a15b38f1/external/mesh-names.belns"
DEFINE NAMESPACE MGI            AS URL "https://raw.githubusercontent.com/pharmacome/terminology/f2f993e599694ab5ce989cc39d789a499f75db99/external/mgi-names.belns"
DEFINE NAMESPACE MIRBASE        AS URL "https://raw.githubusercontent.com/pharmacome/terminology/b46b65c3da259b6e86026514dfececab7c22a11b/external/mirbase-names.belns"
DEFINE NAMESPACE NCBIGENE       AS URL "https://raw.githubusercontent.com/pharmacome/terminology/b46b65c3da259b6e86026514dfececab7c22a11b/external/entrez.belns"
DEFINE NAMESPACE PFAM           AS URL "https://raw.githubusercontent.com/pharmacome/terminology/8ccfed235e418e4c8aa576f9a5ef0f838e794c7f/external/pfam-names.belns"
DEFINE NAMESPACE RGD            AS URL "https://raw.githubusercontent.com/pharmacome/terminology/f2f993e599694ab5ce989cc39d789a499f75db99/external/rgd-names.belns"

# Namespaces hosted on https://github.com/sorgerlab/famplex
# ---------------------------------------------------------
DEFINE NAMESPACE FPLX           AS URL "https://raw.githubusercontent.com/sorgerlab/famplex/e8ae9926ff95266032cb74f77973c84939bffbeb/export/famplex.belns"

# Namespaces hosted on https://arty.scai.fraunhofer.de/artifactory/bel/namespace
# ------------------------------------------------------------------------------
DEFINE NAMESPACE CTO            AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/clinical-trial-ontology/clinical-trial-ontology-1.0.0.belns"
DEFINE NAMESPACE DO             AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/disease-ontology/disease-ontology-20170725.belns"
DEFINE NAMESPACE HP             AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/hp/hp-20171108.belns"
DEFINE NAMESPACE SCOMP          AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/selventa-named-complexes/selventa-named-complexes-20170725.belns"
DEFINE NAMESPACE SFAM           AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/selventa-protein-families/selventa-protein-families-20170725.belns"

# Namespaces defined with regular expressions
# -------------------------------------------
DEFINE NAMESPACE DBSNP          AS PATTERN "rs[0-9]+"
DEFINE NAMESPACE TAXONOMY       AS PATTERN "^\d+$"
DEFINE NAMESPACE UNIPROT        AS PATTERN "^([A-N,R-Z][0-9]([A-Z][A-Z, 0-9][A-Z, 0-9][0-9]){1,2})|([O,P,Q][0-9][A-Z, 0-9][A-Z, 0-9][A-Z, 0-9][0-9])(\.\d+)?$"
DEFINE NAMESPACE PUBCHEM        AS PATTERN "^\d+$"

DEFINE NAMESPACE HM        AS PATTERN ".*"


###############
# Annotations #
###############

# Annotations defined with regular expressions
# --------------------------------------------

DEFINE ANNOTATION Species       AS PATTERN ".*"
DEFINE ANNOTATION TimePoint     AS PATTERN ".*"
DEFINE ANNOTATION Concentration     AS PATTERN ".*"

# Annotations hosted on https://arty.scai.fraunhofer.de/artifactory/bel/annotation
# --------------------------------------------------------------------------------
DEFINE ANNOTATION Anatomy       AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/anatomy/anatomy-20170511.belanno"
DEFINE ANNOTATION CellLine      AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/cell-line/cell-line-20170511.belanno"
DEFINE ANNOTATION CellStructure AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/cell-structure/cell-structure-20170511.belanno"
DEFINE ANNOTATION Cell          AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/cell/cell-20170511.belanno"
DEFINE ANNOTATION Confidence    AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/confidence/confidence-1.0.0.belanno"
DEFINE ANNOTATION Disease       AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/disease/disease-20170511.belanno"
DEFINE ANNOTATION Gender        AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/gender/gender-1.0.0.belanno"
DEFINE ANNOTATION MeSHAnatomy   AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/mesh-anatomy/mesh-anatomy-20170511.belanno"
DEFINE ANNOTATION MeSHDisease   AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/mesh-diseases/mesh-diseases-20170511.belanno"
DEFINE ANNOTATION Subgraph      AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/neurommsig/neurommsig-1.0.3.belanno"
DEFINE ANNOTATION TextLocation  AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/text-location/text-location-1.0.1.belanno"

##############
# Statements #
##############

SET Citation = {"PubMed", "Anesth Analg. 2016 Aug;123(2):274-82. doi: 10.1213/ANE.0000000000001392.", "27308950"} 

SET TextLocation = "Introduction"

SET Evidence = "Several studies have shown that formation of cell-free
hemoglobin (Hb) as a result of hemolysis and Hb-containing
microvesicles increases in stored RBCs with increasing the
storage duration.12,13"

SET Evidence = "Free Hb and Hb present in microvesicles
oxidize vascular nitric oxide (NO) much faster than
RBC-encapsulated Hb to form nitrate.14,15"

p(HGNC:HBB) -> rxn(reactants(a(CHEBI:"nitric oxide")), products (a(CHEBI:nitrate)))

SET Evidence = "Presence of any
cell-free Hb limits NO diffusion from endothelium to
smooth muscle cells for activation of guanylyl cyclase; as
a consequence, mean arterial blood pressure increases.12,15"

SET MeSHAnatomy = "Arteries"
p(HGNC:HBB) -> bp(MESH:"Blood Pressure")

SET Evidence = "Several studies in animals have supported the idea that
infusion of free Hb, stored RBC supernatant (preservation
solution + plasma), and Hb-containing microvesicles
causes vasoconstriction, vascular dysfunction, and vascular
injury.12,16–18"

p(HGNC:HBB) -> bp(GO:vasoconstriction)
p(HGNC:HBB) -> path(MESH:"Vascular System Injuries")

SET Evidence = "In humans, infusion of RBCs stored for longer
durations has been shown to significantly reduce brachial
artery flow-mediated dilation12 and acetylcholine-stimulated
forearm blood flow compared with fresher blood.19,20"

a(HM:"stored erythrocytes") -| a(MESH:Dilatation)

SET Evidence = "In addition to cell-free Hb, membrane structural modification,
externalization of phosphatidylserine,21 decreased
cell membrane deformability,22,23 and increased endothelial
adherence24 could alter vascular NO homeostasis."

SET MeSHAnatomy = "Endothelium, Vascular"
p(HGNC:HBB) -| a(CHEBI:"nitric oxide")
a(CHEBI:"phosphatidyl-L-serine") -| a(CHEBI:"nitric oxide")
bp(GO:"endothelial cell activation") -| a(CHEBI:"nitric oxide")

SET Evidence = "In the current observational study in patients
undergoing multilevel spinal fusion surgery, we tested the
hypothesis that moderate doses of stored RBC transfusions
increase intravascular cell-free Hb and decrease NO availability
in surgical patients."

a(HM:"stored erythrocytes") -> p(HGNC:HBB)
a(HM:"stored erythrocytes") -| a(CHEBI:"nitric oxide")

SET Evidence = "The stored RBCs had approximately
7 times the hemolysis of fresh blood."

a(HM:"stored erythrocytes") -> path(MESH:Hemolysis)

SET TextLocation = "Results"

SET Evidence = "We plotted
plasma nitrite levels in Postop samples by linear regression
against their corresponding heme-NO values and
found that heme-NO species were significantly correlated
(R = 0.525, P < 0.0001) with plasma nitrite, supporting previous
reports28,31–33 that heme-NO species are generated by
reaction of nitrite with deoxyHb (Figure 2B)."

SET MeSHAnatomy = "Plasma"
rxn(reactants(a(CHEBI:nitrite), a(CHEBI:deoxyhemoglobin)), products(a(HM:"heme-NO species")))

SET Evidence = "Thus, we hypothesized that loss of blood during surgery
and hemodilution from crystalloid administration might
contribute to the observed decrease in plasma NOx levels."

SET MeSHAnatomy = "Plasma"
path(MESH:"Blood Loss, Surgical") -| a(CHEBI:nitrite)
path(MESH:"Blood Loss, Surgical") -| a(CHEBI:nitrate)
a(MESH:Hemodilution) -| a(CHEBI:nitrite)
a(MESH:Hemodilution) -| a(CHEBI:nitrate)

SET Evidence = "We calculated the loss of blood and extent of hemodilution
during and after surgery based on the Hct levels for nontransfused
patients (Table 1). Hct was significantly decreased
(by approximately 20%, P = 0.0087) and hemodilution was
significantly increased (P = 0.007) in these patients."

bp(MESH:"Blood Transfusion") -> bp(MESH:Hematocrit)
bp(MESH:"Blood Transfusion") -| a(MESH:Hemodilution)

SET TextLocation = "Discussion"

SET Evidence = "Thus, it was
apparent that blood loss and hemodilution from IV fluids
rather than stored RBC transfusion likely played a role in
the transient decrease of these NO metabolites."

SET MeSHAnatomy = "Plasma"
path(MESH:"Blood Loss, Surgical") -| a(CHEBI:nitrite)
path(MESH:"Blood Loss, Surgical") -| a(CHEBI:nitrate)
a(MESH:Hemodilution) -| a(CHEBI:nitrite)
a(MESH:Hemodilution) -| a(CHEBI:nitrate)

SET Evidence = "Any increase in hemolysis
after transfusion of stored RBCs can be attributed to lysis of
RBCs during storage or after transfusion (Figure 1B)."

a(HM:"stored erythrocytes") -> path(MESH:Hemolysis)
bp(MESH:"Blood Transfusion") -> path(MESH:Hemolysis)

SET Evidence = "An increase in plasma cell-free Hb and
vascular dysfunction also has been shown after transfusion
of autologous RBCs with long storage duration to healthy
human subjects.19,20,37"

bp(MESH:"Blood Transfusion") -> p(HGNC:HBB)
bp(MESH:"Blood Transfusion") -> path(MESH:"Vascular System Injuries")


SET Evidence = "It is unlikely that loss of
nitrite through surgically shed blood and hemodilution
would produce a decrease of 50% from baseline because
continuous synthesis of NO by eNOS should replenish the
loss of nitrite."

p(HGNC:NOS3) -> a(CHEBI:"nitric oxide")

SET Evidence = "In vitro, direct
exposure of vascular strips,38,39 brain tissue,40 polymorphoneutrophils,
41 and thoracic aorta42 to volatile anesthetic
agents such as enflurane, isoflurane, and halothane has been
shown to reversibly inhibit NOS activity."

a(CHEBI:enflurane) -| a(MESH:"Nitric Oxide Synthase")
a(CHEBI:isoflurane) -| a(MESH:"Nitric Oxide Synthase")
a(CHEBI:halothane) -| a(MESH:"Nitric Oxide Synthase")

SET Evidence = "Third, during surgery, patients
were intratracheally intubated, which prevented swallowing
of saliva, which may have interrupted the nitrate-tonitrite
pathway, thus contributing to the observed decrease
in nitrite."

a(MESH:"Salivary Glands") -> rxn(reactants(a(CHEBI:nitrate)), products(a(CHEBI:nitrite)))

SET Evidence = "Several
studies have shown that nitrite is reduced to NO by several
heme proteins under hypoxic conditions, providing an
eNOS-independent source of NO in the vasculature.28,47,48."

SET MeSHDisease = "Hypoxia"
a(MESH:Hemeproteins) -> rxn(reactants(a(CHEBI:nitrite)), products(a(CHEBI:"nitric oxide")))

SET Evidence = "The correlation
of heme-NO with plasma nitrite (Figure 2B) supports
previous studies, which have shown that heme-NO
species are formed when nitrite is reduced by deoxyHb."

SET MeSHAnatomy = "Plasma"
rxn(reactants(a(CHEBI:nitrite), a(CHEBI:deoxyhemoglobin)), products(a(HM:"heme-NO species")))


SET Evidence = "The cell-free Hb in stored RBCs should actually increase
nitrate by oxidizing NO to nitrate."

p(HGNC:HBB) -> a(CHEBI:nitrate)

SET Evidence = "The alternate explanation for a postoperative
decrease in nitrate may be related to blood loss
during and after surgery as well as hemodilution."

path(MESH:"Blood Loss, Surgical") -| a(CHEBI:nitrate)
a(MESH:Hemodilution) -| a(CHEBI:nitrate)

SET Evidence = "Studies have shown
that nitrate concentrates in saliva and is reduced to nitrite
by mouth commensal bacterial nitrate reductase and then
nitrite is reduced to NO under acidic conditions of the stomach.
28,51,52"

SET MeSHAnatomy = "Saliva"
a(MESH:"Salivary Glands") -> rxn(reactants(a(CHEBI:nitrate)), products(a(CHEBI:nitrite)))
SET MeSHAnatomy = "Stomach"
rxn(reactants(a(CHEBI:nitrite)), products(a(CHEBI:"nitric oxide")))

UNSET TextLocation 
############