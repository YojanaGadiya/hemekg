

#######################
# Document Properties #
#######################

SET DOCUMENT Licenses    = "MIT License"
SET DOCUMENT ContactInfo = "farah.humayun@scai-extern.fraunhofer.de"

SET DOCUMENT Name        = "heme_24486321.bel"
SET DOCUMENT Version     = "1.0.0"
SET DOCUMENT Authors     = "Farah Humayun"
SET DOCUMENT Description = "Heme BEL model is a disease model based on Biological Expression Language (BEL) containing the core mechanisms of heme as modulator in Hemolytic diseases such as sickle-cell anemia, beta-thalassemia, malaria and autoimmune hemolytic anemia."

##############
# Namespaces #
##############

# Namespaces hosted on https://github.com/pharmacome/terminology
# --------------------------------------------------------------
DEFINE NAMESPACE CHEBI          AS URL "https://raw.githubusercontent.com/pharmacome/terminology/b46b65c3da259b6e86026514dfececab7c22a11b/external/chebi-names.belns"
DEFINE NAMESPACE DRUGBANK       AS URL "https://raw.githubusercontent.com/pharmacome/terminology/b46b65c3da259b6e86026514dfececab7c22a11b/external/drugbank-names.belns"
DEFINE NAMESPACE ECCODE         AS URL "https://raw.githubusercontent.com/pharmacome/terminology/b46b65c3da259b6e86026514dfececab7c22a11b/external/ec-code.belns"
# DEFINE NAMESPACE FB           AS URL "https://raw.githubusercontent.com/pharmacome/terminology/80171ae62cf43aa1fc8a6c326b94537ab342458c/external/fb-names.belns"
DEFINE NAMESPACE HBP            AS URL "https://raw.githubusercontent.com/pharmacome/terminology/3074b85b858455d8eeb76cfcdef685ced19bbe11/export/hbp-names.belns"
DEFINE NAMESPACE HGNC           AS URL "https://raw.githubusercontent.com/pharmacome/terminology/b46b65c3da259b6e86026514dfececab7c22a11b/external/hgnc-names.belns"
DEFINE NAMESPACE HGNCGENEFAMILY AS URL "https://raw.githubusercontent.com/pharmacome/terminology/3074b85b858455d8eeb76cfcdef685ced19bbe11/external/hgnc.genefamily-names.belns"
DEFINE NAMESPACE GO             AS URL "https://raw.githubusercontent.com/pharmacome/terminology/b46b65c3da259b6e86026514dfececab7c22a11b/external/go-names.belns"
DEFINE NAMESPACE INTERPRO       AS URL "https://raw.githubusercontent.com/pharmacome/terminology/f2f993e599694ab5ce989cc39d789a499f75db99/external/interpro-names.belns"
DEFINE NAMESPACE MESH           AS URL "https://raw.githubusercontent.com/pharmacome/terminology/01c9daa61012b37dd0a1bc962521ba51a15b38f1/external/mesh-names.belns"
DEFINE NAMESPACE MGI            AS URL "https://raw.githubusercontent.com/pharmacome/terminology/f2f993e599694ab5ce989cc39d789a499f75db99/external/mgi-names.belns"
DEFINE NAMESPACE MIRBASE        AS URL "https://raw.githubusercontent.com/pharmacome/terminology/b46b65c3da259b6e86026514dfececab7c22a11b/external/mirbase-names.belns"
DEFINE NAMESPACE NCBIGENE       AS URL "https://raw.githubusercontent.com/pharmacome/terminology/b46b65c3da259b6e86026514dfececab7c22a11b/external/entrez.belns"
DEFINE NAMESPACE PFAM           AS URL "https://raw.githubusercontent.com/pharmacome/terminology/8ccfed235e418e4c8aa576f9a5ef0f838e794c7f/external/pfam-names.belns"
DEFINE NAMESPACE RGD            AS URL "https://raw.githubusercontent.com/pharmacome/terminology/f2f993e599694ab5ce989cc39d789a499f75db99/external/rgd-names.belns"

# Namespaces hosted on https://github.com/sorgerlab/famplex
# ---------------------------------------------------------
DEFINE NAMESPACE FPLX           AS URL "https://raw.githubusercontent.com/sorgerlab/famplex/e8ae9926ff95266032cb74f77973c84939bffbeb/export/famplex.belns"

# Namespaces hosted on https://arty.scai.fraunhofer.de/artifactory/bel/namespace
# ------------------------------------------------------------------------------
DEFINE NAMESPACE CTO            AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/clinical-trial-ontology/clinical-trial-ontology-1.0.0.belns"
DEFINE NAMESPACE DO             AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/disease-ontology/disease-ontology-20170725.belns"
DEFINE NAMESPACE HP             AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/hp/hp-20171108.belns"
DEFINE NAMESPACE SCOMP          AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/selventa-named-complexes/selventa-named-complexes-20170725.belns"
DEFINE NAMESPACE SFAM           AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/selventa-protein-families/selventa-protein-families-20170725.belns"

# Namespaces defined with regular expressions
# -------------------------------------------
DEFINE NAMESPACE DBSNP          AS PATTERN "rs[0-9]+"
DEFINE NAMESPACE TAXONOMY       AS PATTERN "^\d+$"
DEFINE NAMESPACE UNIPROT        AS PATTERN "^([A-N,R-Z][0-9]([A-Z][A-Z, 0-9][A-Z, 0-9][0-9]){1,2})|([O,P,Q][0-9][A-Z, 0-9][A-Z, 0-9][A-Z, 0-9][0-9])(\.\d+)?$"
DEFINE NAMESPACE PUBCHEM        AS PATTERN "^\d+$"

DEFINE NAMESPACE HM        AS PATTERN ".*"


###############
# Annotations #
###############

# Annotations defined with regular expressions
# --------------------------------------------

DEFINE ANNOTATION Species       AS PATTERN ".*"
DEFINE ANNOTATION TimePoint     AS PATTERN ".*"
DEFINE ANNOTATION Concentration     AS PATTERN ".*"

# Annotations hosted on https://arty.scai.fraunhofer.de/artifactory/bel/annotation
# --------------------------------------------------------------------------------
DEFINE ANNOTATION Anatomy       AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/anatomy/anatomy-20170511.belanno"
DEFINE ANNOTATION CellLine      AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/cell-line/cell-line-20170511.belanno"
DEFINE ANNOTATION CellStructure AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/cell-structure/cell-structure-20170511.belanno"
DEFINE ANNOTATION Cell          AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/cell/cell-20170511.belanno"
DEFINE ANNOTATION Confidence    AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/confidence/confidence-1.0.0.belanno"
DEFINE ANNOTATION Disease       AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/disease/disease-20170511.belanno"
DEFINE ANNOTATION Gender        AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/gender/gender-1.0.0.belanno"
DEFINE ANNOTATION MeSHAnatomy   AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/mesh-anatomy/mesh-anatomy-20170511.belanno"
DEFINE ANNOTATION MeSHDisease   AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/mesh-diseases/mesh-diseases-20170511.belanno"
DEFINE ANNOTATION Subgraph      AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/neurommsig/neurommsig-1.0.3.belanno"
DEFINE ANNOTATION TextLocation  AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/text-location/text-location-1.0.1.belanno"

##############
# Statements #
##############

SET Citation = {"PubMed", "Free Radic Biol Med. 2014 Apr;69:265-77. doi: 10.1016/j.freeradbiomed.2014.01.030. Epub 2014 Jan 30..", "24486321"} 

SET TextLocation = "Abstract"

SET Evidence = "Haptoglobin (Hp) is an abundant and conserved plasma glycoprotein, which binds acellular adult
hemoglobin (Hb) dimers with high affinity and facilitates their rapid clearance from circulation
following hemolysis."

p(HGNC:HP) -| p(HGNC:HBB)

SET TextLocation = "Introduction"

SET Evidence = "For example, autooxidation of oxyhemoglobin (HbO2)
or oxymyoglobin (MbO2) produces met-Hb or met-Mb, respectively, and superoxide
radicals (O2•−, HO2•) via electron transfer [3,4]."

a(CHEBI:oxyhemoglobin) -> a(CHEBI:methemoglobin)

SET Evidence = "Superoxide spontaneously dismutates into
O2 and hydrogen peroxide (H2O2), which can then react with Fe(II) or Fe(III) within Hb to
produce ferryl Hb (Fe(IV)oxo species; e.g., Fe4+=O2−) [5]."

rxn(reactants(a(CHEBI:dioxygen), a(CHEBI:"hydrogen peroxide"), a(CHEBI:"iron(2+)")), products(a(MESH:ferrylhemoglobin)))

SET Evidence = "Haptoglobin (Hp) is a Hb-scavenging plasma glycoprotein which binds non-covalently to
hemoglobin dimers that are generated by dissociation of acellular Hb tetramers after
hemolysis [6]."

p(HGNC:HP) -| p(HGNC:HBB)

SET Evidence = "For example, Hp 2-2 has been associated with an increased susceptibility to
diabetic cardiovascular disease; uptake of Hb-Hp(1-1) complexes by CD163 receptors is
reportedly faster and its binding results in increased concentrations of anti-inflammatory
mediators than Hb-Hp(2-2) complexes; and the angiogenic potency of Hp 2-2 is reportedly
greater than that of Hp 1-1 [18]."

a(MESH:"haptoglobin-hemoglobin complex") -| path(MESH:Inflammation)

SET Evidence = "Hemolysis and the transfusion of banked blood or Hb-based therapeutics can result in
varying quantities of circulating acellular Hb which can induce life threatening radical
generating reactions in patients with a compromised vascular system [60]."

SET TextLocation = "Discussion"

path(MESH:Hemolysis) pos p(HGNC:HBB)
p(HGNC:HBB) pos path(MESH:"Vascular System Injuries")

SET Evidence = "Ferryl heme and associated protein
radicals with high redox potentials (~1.0 V) can induce a wide variety of oxidative reactions
that affect the protein and nearby molecules [2]."

a(CHEBI:heme) pos a(HM:"oxidative reactions")

SET Evidence = "Third, exposure to extracellular Hb, in the
form of oxygen therapeutics or when Hb is released from old red blood cells, have also been
reported to induce oxidative toxicity in kidney and brain tissues [65]."

p(HGNC:HBB) pos path(MESH:"Kidney Diseases")
p(HGNC:HBB) pos path(MESH:"Brain Diseases")

SET Evidence = "Hp is the first-line
scavenger that binds and accelerates the clearance of Hb in the circulation, although the
macrophage CD163 receptor has also been the focus of several recent investigations [66–
68]."

p(HGNC:HP) -| p(HGNC:HBB)

SET Evidence = "Probably the most important Hp-mediated abrogation of Hb toxicity is stabilization of heme
within the central cavity of the Hb subunits, which almost completely prevents its
dissociation and subsequent free heme-mediated oxidative reactions and inflammatory
responses."

p(HGNC:HP) -| a(HM:"oxidative reactions")
p(HGNC:HP) -| path(MESH:Inflammation)

SET Evidence = "First, addition of
exogenous Hp both markedly inhibits heme loss and second, as a result of this inhibition,
globin precipitation is prevented."

p(HGNC:HP) pos a(CHEBI:heme)

SET Evidence = "These findings are consistent with a recent study which showed that both Hp
and hemopexin (heme scavenger) were equally effective in preventing vasoocclusion in a
sickle cell mouse model infused with Hb [73]."

SET MeSHDisease = "Anemia, Sickle Cell"
p(MGI:Hp) -| path(HM:"vaso-occlusive crisis")
p(MGI:Hpx) -| path(HM:"vaso-occlusive crisis")

SET Evidence = "In that study, heme was shown to specifically
bind to endothelial Toll-like receptors (TLR4) and trigger a cascade of inflammatory
responses, which could be attributed to oxidation and degradation of cell-free Hb [73]."

deg(p(HGNC:HBB)) -> a(CHEBI:heme)
complex(a(CHEBI:heme), p(MGI:Tlr4)) pos path(MESH:Inflammation)

SET Evidence = "Hp
bound metHb and prevented heme loss and hemopexin captures any free heme released from
metHb during its denaturation."

deg(a(CHEBI:methemoglobin)) -> a(CHEBI:heme)
p(MGI:Hpx) -| a(CHEBI:heme)

SET Evidence = "This result is
in accord with recent work, which showed that Hp binding to Hb prevents oxidative damage
to the globin."

a(MESH:"haptoglobin-hemoglobin complex") -| a(HM:"oxidative reactions")

UNSET TextLocation 
############